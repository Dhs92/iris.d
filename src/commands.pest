WHITESPACE = _{ " " }
NUL = { "\0" }
// "[", "]", "\", "`", "_", "^", "{", "|", "}"
special = _{ '\u{5B}'..'\u{60}' | '\u{7B}'..'\u{7D}' }
nospcrlf = _{ !(NUL | NEWLINE | WHITESPACE | ":") ~ ANY }

shortname = @{ !"-" ~ (ASCII_ALPHA | ASCII_DIGIT | "-")+ ~ !"-" }
message = { (":" ~ prefix)? ~ command ~ (params)? }
command = @{ ASCII_ALPHA+ | ASCII_DIGIT{3} }
params = _{ (middle{0,14} ~ (":" ~ trailing)?) | (middle{0,14} ~ (":"? ~ trailing)?)}
trailing = @{ (":" | WHITESPACE | nospcrlf)* }
middle = @{ nospcrlf ~ (":" | nospcrlf)* }
mode = @{ (("+" | "-" ) ~ ( "i" | "w" | "o" | "O" | "r")* ) }
modemask = { ASCII_DIGIT }
prefix = { (nickname ~ (("!" ~ user)? ~ "@" ~ host)?) | servername }
password = @{ (!(NEWLINE | WHITESPACE | NUL) ~ ANY)* }
distribution = @{ ("*" ~ shortname? ~ ("." ~ shortname)?) | hostname }
reserved = _{ "*" }

// wildcards
mask = { (nowild | noesc ~ wildone | noesc ~ wildmany)* }
wildone = { "?" }
wildmany = { "*" }
nowild = { !(NUL | "*" | "?")* ~ ANY }
noesc = { !(NUL | "\\")* ~ ANY }
matchone = { ANY }
matchmany = { matchone* }

// messaging
target = { nickname | servername }
msgtarget = @{ msgto ~ ("," ~ msgto)* }
msgto = ${ channel | (user ~ "%" ~ host) 
        | (user ~ ("%" ~ host)? ~ "@" ~ servername) | targetmask
        | nickname | (nickname ~ "!" ~ user ~ "@" ~ host)
         }

// naming
nickname = @{ (ASCII_ALPHA | special) ~ (ASCII_ALPHA | ASCII_DIGIT | special | "-"){0,8} }
servername = { hostname }
// all octets except for NUL, CR, LF, " " and "@"
//user = @{ ('\u{01}'..'\u{09}' | '\u{0B}'..'\u{0C}' | '\u{0E}'..'\u{1F}' | '\u{21}'..'\u{3F}' | '\u{41}'..'\u{FF}')+ }
user = @{ (!(NUL | NEWLINE | WHITESPACE | "@" | ",") ~ ANY)+ }
channel = { ("#" | "+" | ("!" ~ channelid) | "&") ~ chanstring ~ (":" ~ chanstring)? }
channelid = { (ASCII_ALPHA | ASCII_DIGIT){5} }
chanstring = @{ ('\u{01}'..'\u{06}' | '\u{08}'..'\u{09}' | '\u{0B}'..'\u{0C}' | '\u{0E}'..'\u{1F}' | '\u{21}'..'\u{2B}' | '\u{2D}'..'\u{39}' | '\u{3B}'..'\u{FF}'){,49} }
targetmask = { "$" | "#" ~ mask }

// network
host = { hostname | hostaddr }
hostname = @{ shortname ~ ("." ~ shortname)* }
hostaddr = _{ ipv4 | ipv6 }
ipoctet = @{ ASCII_DIGIT{1,3} }
ipv4 = @{ (ipoctet ~ "." | ipoctet){4} }
ipv6 = @{ ASCII_HEX_DIGIT+ ~ (ASCII_HEX_DIGIT ~ ":"){7} | "0:0:0:0:0:" ~ ( "0" | "FFFF" ) ~ ":" ~ ipv4 }
